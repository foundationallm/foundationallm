param environmentName string
param location string
param project string
param timestamp string = utcNow()
param vnetId string

/*
This block defines the private DNS zone mappings for various services.
Each service is associated with a specific private DNS zone.
The private DNS zone names are constructed using placeholders for location and environment suffixes.
*/
var privateDnsZone = {
  aks: 'privatelink.${location}.azmk8s.io'
  blob: 'privatelink.blob.${environment().suffixes.storage}'
  cognitiveservices: 'privatelink.cognitiveservices.azure.com'
  configuration_stores: 'privatelink.azconfig.io'
  cosmosdb: 'privatelink.documents.azure.com'
  cr: 'privatelink.azurecr.io'
  cr_region: '${location}.privatelink.azurecr.io'
  dfs: 'privatelink.dfs.${environment().suffixes.storage}'
  file: 'privatelink.file.${environment().suffixes.storage}'
  gateway: 'privatelink.azure-api.net'
  gateway_developer: 'developer.azure-api.net'
  gateway_management: 'management.azure-api.net'
  gateway_portal: 'portal.azure-api.net'
  gateway_public: 'azure-api.net'
  gateway_scm: 'scm.azure-api.net'
  grafana: 'privatelink.grafana.azure.com'
  monitor: 'privatelink.monitor.azure.com'
  openai: 'privatelink.openai.azure.com'
  prometheus: 'privatelink.${location}.prometheus.monitor.azure.com'
  queue: 'privatelink.queue.${environment().suffixes.storage}'
  search: 'privatelink.search.windows.net'
  sites: 'privatelink.azurewebsites.net'
  sql_server: 'privatelink${environment().suffixes.sqlServerHostname}'
  table: 'privatelink.table.${environment().suffixes.storage}'
  vault: 'privatelink.vaultcore.azure.net'
}

/*
  This block of code generates an output array called 'ids' that contains information about private DNS zones.
  Each item in the array includes the ID and name of a private DNS zone.
*/
output ids array = [for (zone, i) in items(privateDnsZone): {
  id: dns[i].outputs.id
  key: dns[i].outputs.key
}]

/*
  This Bicep code module deploys private DNS zones using the 'dns' module.
  It iterates over the 'privateDnsZone' items and creates a private DNS zone for each item.
  The deployment name of each private DNS zone is generated by appending the timestamp to the zone value.
  The 'vnetId' parameter specifies the ID of the virtual network associated with the private DNS zone.
  The 'zone' parameter specifies the name of the private DNS zone.
  The 'tags' object contains metadata tags for the private DNS zone, including the environment name, IaC type, project name, and purpose.
*/
module dns './modules/dns.bicep' = [for zone in items(privateDnsZone): {
  name: '${zone.value}-${timestamp}'
  params: {
    key: zone.key
    vnetId: vnetId
    zone: zone.value

    tags: {
      Environment: environmentName
      IaC: 'Bicep'
      Project: project
      Purpose: 'Networking'
    }
  }
}]
